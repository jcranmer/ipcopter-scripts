# Plot results

#set term png enhanced # size 800,1024
set term pdfcairo enhanced size 3.125in,2.34375in

set datafile separator ','

set grid

set xtics font ",12" offset "0,-100"
set ylabel "Normalized system call latency"
set yrange [0:6]
set bmargin 2.5


set boxwidth 0.9
set style fill solid 0.5 border

set grid noxtics
#set key outside above

set linetype 1 linecolor rgb "#e41a1c"
set linetype 2 linecolor rgb "#377eb8"
set linetype 3 linecolor rgb "#4daf4a"
set linetype 4 linecolor rgb "#984ea3"

set style data histogram
set style histogram cluster gap 1.8
set xtics scale 0
plot \
    "lmbench.csv" using ($2/$2):xticlabels(1) title columnheader, \
    "lmbench.csv" using ($3/$2):xticlabels(1) title columnheader, \
    "lmbench.csv" using ($0 - ($3/$2 < 1.5 ? 1 : 0.8)):($3 < $2 ? 1.1 : $3/$2 + 0.1):3 \
      w labels notitle font ",14" center noenhanced
