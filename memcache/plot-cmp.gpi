# Plot results

set term png enhanced # size 800,1024

set datafile separator ','

set grid

set xlabel "% Get Probability"
set ylabel "Ops/Sec"


set boxwidth 0.9
set style fill solid 0.5 border

threads="`tail -n+2 memcache.baseline.csv|awk -F ',' '{print $1, "\n"}'|sort -u`"

#set multiplot layout words(threads),1

set key outside above

#do for [i=1:words(threads)] {
#plot "memcache.baseline.csv" using (($1 == word(threads, i))?$2:1/0):6 with linespoints title "Baseline - " . word(threads,i) . " Threads", \
#     "memcache.ipcd.csv" using (($1 == word(threads, i))?$2:1/0):6 with linespoints title "IPCD - " . word(threads,i) . " Threads"
#}

colors="dark-violet #009e73 #56b4e9 #e69f00 #f0e442 #0072b2 #e51e10 black gray50"

set for [i=1:words(threads)] style line (2*i) pt 4 lt rgb word(colors, i)
set for [i=1:words(threads)] style line (2*i+1) pt 8 lt rgb word(colors, i)

plot for [i=1:words(threads)] "memcache.baseline.csv" using (($1 == word(threads, i))?$2:1/0):6 with linespoints title "Baseline - " . word(threads,i) . " Threads" ls (2*i), \
     for [i=1:words(threads)] "memcache.ipcd.csv" using (($1 == word(threads, i))?$2:1/0):6 with linespoints title "IPCD - " . word(threads,i) . " Threads" ls (2*i+1)

#unset multiplot
