# Plot results

set term png enhanced # size 800,1024

set datafile separator ','

set grid

set xlabel "% Get Probability"
set ylabel "Ops/Sec"


set boxwidth 0.9
set style fill solid 0.5 border

threads="`tail -n+2 memcache.baseline.csv|awk -F ',' '{print $1, "\n"}'|sort -u`"

set key outside above

plot for [i=1:words(threads)] "memcache.baseline.csv" using (($1 == word(threads, i))?$2:1/0):6 with linespoints title "Baseline - " . word(threads,i) . " Threads" pt i+1, \
     for [i=1:words(threads)] "memcache.ipcd.csv" using (($1 == word(threads, i))?$2:1/0):6 with linespoints title "IPCD - " . word(threads,i) . " Threads" pt i+1
