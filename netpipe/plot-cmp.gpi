# Plot results

set term pdf size 4in,3in #png enhanced

set datafile separator ','

set logscale x 2
set format x "%.0b%BB"
set xtics 1,16
#set xtics autofreq

set grid noxtics ytics

#set xrange [1:2**24]
set xrange [2**4:2**24]
set yrange [0:3]
#set yrange [0:5]

set xlabel "Transmit size"
set ylabel "Slipstream throughput ratio"

set style line 1 linecolor rgb "#e41a1c" lw 2 pt 8 ps 0.5
set style line 2 linecolor rgb "#377eb8" lw 2 pt 4 ps 0.5
set style line 3 linecolor rgb "#4daf4a" lw 2
set style line 4 linecolor rgb "#984ea3" lw 2

# Aggregate values for each pertubation value
! paste -d, netpipe.ipcd.csv netpipe.baseline.csv | sort -k3,3 -t, -n > temp.csv
! awk -F ',' 'BEGIN { bytes = 0; ipcd = 0; baseline =0; n = 1 } { if ($3 != bytes) { printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); ipcd = 0; baseline=0;n=0 } bytes = $3; ipcd += $4; baseline += $9; n+= 1} END {  printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); }' temp.csv > temp2.csv

! paste -d, netpipe-java.ipcd.csv netpipe-java.baseline.csv | sort -k4,4 -t, -n > temp.csv
! awk -F ',' 'BEGIN { bytes = 0; ipcd = 0; baseline =0; n = 1 } { if ($4 != bytes) { printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); ipcd = 0; baseline=0;n=0 } bytes = $4; ipcd += $3; baseline += $9; n+= 1} END {  printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); }' temp.csv > temp3.csv

plot 'temp2.csv' using ($1):($2/$3) with linespoints title "NetPIPE-C" ls 1, \
     'temp3.csv' using ($1):($2/$3) with linespoints title "NetPIPE-Java" ls 2
