# Plot results

set term png enhanced

set datafile separator ','

set logscale x 2
set format x "%.0b%BB"
set xtics 1,16
#set xtics autofreq


set xrange [2**4:2**24]
set yrange [0:3]

set xlabel "Transmit size"
set ylabel "Slipstream throughput ratio"

set style line 1 linecolor rgb "#fc8d62"
set style line 2 linecolor rgb "#377eb8"
set style line 3 linecolor rgb "#4daf4a"
set style line 4 linecolor rgb "#984ea3"

set grid noxtics ytics
# Aggregate values for each pertubation value
! paste -d, netpipe.ipcd.csv netpipe.baseline.csv | sort -k3,3 -t, -n > temp.csv
! awk -F ',' 'BEGIN { bytes = 0; ipcd = 0; baseline =0; n = 1 } { if ($3 != bytes) { printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); ipcd = 0; baseline=0;n=0 } bytes = $3; ipcd += $4; baseline += $9; n+= 1} END {  printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); }' temp.csv > temp2.csv

! paste -d, netpipe-java.ipcd.csv netpipe-java.baseline.csv | sort -k4,4 -t, -n > temp.csv
! awk -F ',' 'BEGIN { bytes = 0; ipcd = 0; baseline =0; n = 1 } { if ($4 != bytes) { printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); ipcd = 0; baseline=0;n=0 } bytes = $4; ipcd += $3; baseline += $9; n+= 1} END {  printf("%d,%f,%f\n",bytes, ipcd/n, baseline/n); }' temp.csv > temp3.csv

plot 'temp2.csv' u ($1):($2/$3) w lines t 'NetPIPE-C', \
     'temp3.csv' u ($1):($2/$3) w lines ls 2 t 'NetPIPE-Java'
